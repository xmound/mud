#CLASS {fight_dragon} OPEN

#ACTION {(%1)告诉你：report}
{
	#script {foo} {python scripts/report.py 'fight_dragon' '$login_id'};
	#variable status_report $foo[1];
	#if {"$skill_to_learn" == ""}
	{
		tell %1 $status_report目前武学$current_wx, 潜能$current_qn, 未学习技能
	};
	#else
	{
		tell %1 $status_report目前武学$current_wx, 潜能$current_qn, 正学习技能：$skill_to_learn。
	}
}
{5}

#ACTION {^LOOKS LIKE YOU HAVE BEEN IDLING FOR A WHILE?}
{
	stop;
	#delay 5 {start}
}
{5}

#ACTION {^SUCCESSFULLY GOT OUT FROM SHELITA.}
{
	#delay 5 {relocate}
}
{5}

#ACTION {^忽然一阵黄风呼啸而来，你身不由己被卷了进去！}
{
	stop;
	#delay 5 {get_out_from_shelita}
}
{5}

#ACTION {{XYJ RECONNECTION DONE.|XYJ NEW CONNECTION ESTABLISHED.}}
{
	stop;
	start
}
{5}

#ALIAS {reset}
{
	stop;
	treload fight_dragon_$login_id;
	hp;
	start_fight
}
{5}

#ALIAS {reset_idle_prevention_ticker}
{
	#ticker fight_dragon_idle_prevention {#showme {LOOKS LIKE YOU HAVE BEEN IDLING FOR A WHILE?};} {600}
}
{5}

#ALIAS {start}
{
	#echo {<fff> 请确认各技能！};
	#echo {<fff> 请确认各技能！};
	#delay 2 {#echo {<fff> 请确认各技能！};#echo {<fff> 请确认各技能！};};
	hp;
	start_setup;
	reset_idle_prevention_ticker
}
{5}

#ALIAS {start_fight}
{
	#class fight_dragon_fight_inner open;
	#variable count_dragon 0;
	#action {尸体%*Corpse} {#line {gag};};
	#action {骸骨%*Skeleton} {#line {gag};};
	#alias check_dragon_status {#var count_dragon 0;#var practice_needed 0;look;hp;skills;set brief check_dragon_number;};
	#alias turn_on_fight_dragon_tick {reset_idle_prevention_ticker;#tick fight_dragon {check_dragon_status;} {30};};
	#action {{^□|}%S%s(%S)%s-%*%d/%*} {#if {"%%4" == "$skill_to_learn"} {#var skill_to_upgrade_chinese %%2;#var current_skill_level %%7;#var current_skill_exp %%8;#MATH exp_to_next_level {($current_skill_level + 1 ) * ($current_skill_level + 1)};#nop SHOWME {练习状态prompt};#if {$current_skill_exp <= $exp_to_next_level} {#var practice_neeeded {0};}; #elseif {$current_skill_exp > $exp_to_next_level} {#var practice_needed {1};#var skill_to_practice {$skill_to_learn};#var skill_to_practice_chinese {$skill_to_upgrade_chinese};};}; #elseif { "%%4" == "$skill_to_learn2"} {#nop ;}; #else {#line {gag};};};
	#action {你的「基本%*」进步了！} {#if {$abandon_skills == 1} {#if {"$weapon_for_fight" == "rake"} {abandon rake;abandon rake};#if {"$weapon_for_fight" == "sword"} {abandon sword;abandon sword};};};
	#action {^设定环境变数%*check_dragon_number} {#if {$count_dragon == 0} {stop;#delay 5 {start_setup;};}; #elseif {$current_drink < 200 || $current_food < 200} {$path_from_fight_to_eat;surrender;chi;$path_from_eat_to_fight;sh;#loop 1 $count_dragon cnt {fight dragon $cnt;kill dragon $cnt;};}; #elseif {$percent_hp < 95} {#if {$percent_hp < 50} {quitAction;}; #else {$path_one_step_move;surrender;exert heal;er;$path_one_step_back;#loop 1 $count_dragon cnt {fight dragon $cnt};};};#elseif {$count_dragon < $dragons_to_fight} {start_summon;}; #elseif {$current_qn > 90 && $learn_skills == 1 && $practice_needed == 0} {$path_from_fight_to_master;surrender;#2 learn $skill_to_learn from $master for 10000;re;#2 learn $skill_to_learn2 from $master for 10000;re;$path_from_master_to_fight;#loop 1 $count_dragon cnt {fight dragon $cnt};};  #elseif {$practice_needed == 100} {start_practice;}; #elseif {$current_nl_percent < 20} {random;surrender;er;fly sky;stop;start_setup;};turn_on_fight_dragon_tick;#if {$previous_wx > 0} {#math wx_gain {$current_wx - $previous_wx};#nop format fight_dragon_status {%t\t%d} {%Y-%m-%d %H:%M:%S} {$wx_gain};#nop format filename {logs/fight_dragon_rewards_%s.txt} {$login_id};#nop line {log} {$filename} {$fight_dragon_status};#format current_timestamp {%t} {%Y-%m-%d %H:%M:%S};#system {sqlite3 db/records.db 'insert into fight_dragon_log values("${current_timestamp}","${login_id}","wx_gain","${wx_gain}")'};#math previous_wx  {$current_wx};}; #else {#math previous_wx {$current_wx};};#if {$previous_qn > -1} {#math qn_gain {$current_qn - $previous_qn};#format current_timestamp {%t} {%Y-%m-%d %H:%M:%S};#system {sqlite3 db/records.db 'insert into fight_dragon_log values("${current_timestamp}","${login_id}","qn_gain","${qn_gain}")'};#math previous_qn {$current_qn};}; #else {#math previous_qn {$current_qn};};} {4};
	#action {^Successfully summoned a dragon.} {#math count_dragon {$count_dragon+1};#delay 7 {$path_one_step_move;surrender;$path_one_step_back;#loop 1 $count_dragon cnt {fight dragon $cnt;kill dragon $cnt;};};turn_on_fight_dragon_tick;#nop format fight_dragon_status {%t\tSummoned a dragon} {%Y-%m-%d %H:%M:%S};#nop format filename {logs/fight_dragon_rewards_%s.txt} {$login_id};#nop line {log} {$filename} {$fight_dragon_status};#format current_timestamp {%t} {%Y-%m-%d %H:%M:%S};#system {sqlite3 db/records.db 'insert into fight_dragon_log values("${current_timestamp}","${login_id}","dragon_summoned",NULL)'};};
	#prompt {^设定环境变数%*check_dragon_number} {$status_bar_prefix|<fca>Fighting Dragon|目前共$count_dragon只龙|目前武学$current_wx|目前潜能$current_qn|正学习$skill_to_learn} {2};
	#action {^%?%?%?%?%*龙%*Dragon} {#math count_dragon {$count_dragon + 1}};
	#action {^Failed to summon dragon.} {#delay 5 {check_dragon_status;};turn_on_fight_dragon_tick;};
	#action {^%?%?%?%?你似乎十分疲惫} {$path_one_step_move;surrender;#3 exert heal;er;re;$path_one_step_back;#loop 1 $count_dragon cnt {fight dragon $cnt};} {4};
	#nop action {^%?%?%?%?你气喘嘘嘘} {$path_one_step_move;surrender;er;#path_one_step_back;#loop 1 $count_dragon cnt {fight dragon $cnt};} {4};
	#action {^%?%?%?%?%?%?%?%?%?%?龙气喘嘘嘘} {#loop 1 $count_dragon cnt {#1 {exert recover dragon $cnt};};};
	#action {^你要向谁求教？} {#format current_timestamp {%t} {%Y-%m-%d %H:%M:%S};#system {sqlite3 db/records.db 'insert into fight_dragon_log values("${current_timestamp}","${login_id}","master_absent",NULL)'};};
	#action {^%*和你切磋了一会儿%*，你似乎有所领悟。} {#format current_timestamp {%t} {%Y-%m-%d %H:%M:%S};#nop system {sqlite3 db/records.db 'insert into fight_dragon_log values("${current_timestamp}","${login_id}","learned%%2",NULL)'};};
	#action {^%?%?%?%?%?%?%你一次输入} {#format current_timestamp {%t} {%Y-%m-%d %H:%M:%S};#system {sqlite3 db/records.db 'insert into fight_dragon_log values("${current_timestamp}","${login_id}","maxed_out_inpus",NULL)'};stop;#ignore actions on;#untick fight_dragon;#delay 5 {#ignore actions off;start;};};
	#action {^%?%?%?%?%?看来该找机会逃跑了} {#format current_timestamp {%t} {%Y-%m-%d %H:%M:%S};#system {sqlite3 db/records.db 'insert into fight_dragon_log values("${current_timestamp}","${login_id}","escaped",NULL)'};random;surrender;er;fly sky;stop;start_setup;};
	#action {{你对准%*深深吸了几口气运将过去|对准你，深深吸了几口气运将过去}} {#line {gag};};
	#class fight_dragon_fight_inner close;
	check_dragon_status
}
{5}

#ALIAS {start_setup}
{
	#class fight_dragon_setup_inner open;
	#nop action {^{一名马官牵着你的坐骑走了过来。|监副对你说：怎么？骑驴找驴？}} {mm;#delay 5 {$path_to_fight;l};};
	#nop action {^{你已经%%*在%*上了！|你潇洒地一个纵身，稳稳地%%*在%%*上！}} {};
	#nop action {^$place_to_fight} {#class fight_dragon_setup_inner kill;start_fight};
	#alias go_buy_weapon {#if {"$weapon_for_fight" == "rake"} {fly tianzhu;#8 e;w;w;w;n;drop rake;buy zhupa;s;unwield bang;unwield sword;unwield fork;wield rake;}; #elseif {"$weapon_for_fight" == "sword"} {fly moon;get guishuzhi;unwield bang;unwield sword;unwield fork;wield sword;}; #elseif {"$weapon_for_fight" == "unarmed"} {unwield all;};};
	#alias go_buy_weapon_practice {fly changan;w;s;#delay 5 {withdraw 1 gold;#delay 5 {n;e;s;w;#delay 2 {drop $weapon_for_practice;buy $weapon_for_practice;#delay 3 {e;#showme {BOUGHT WEAPON};};};};};};
	#action {^守门人拦住了你：帮派总堂重地闲杂人免进，无帮派人士请去} {enforce 100;kill guard;};
	#action {^守门人死了。} {enforce 0;hd;l;};
	#action {^南天极 -} {enforce 0;kill hd guard;start_summon};
	#action {^Failed to summon dragon.} {#delay 5 {l;};};
	#action {^Successfully summoned a dragon.} {enforce 0;#class fight_dragon_setup_inner kill;#delay 6 {$path_from_reset_to_fight;kill dragon;start_fight;};};
	#action {^RELOCATED TO STONE SUCCESSFULLY.} {$fight_enables;#delay 2 {go_buy_weapon;#delay 2 {go_buy_weapon_practice};};};
	#action {^BOUGHT WEAPON} {#var previous_wx {0};#var previous_qn {-1};enforce 0;set brief_all;wimpy 40;fly changan;#6 s;e;nu;hd;l;};
	#class fight_dragon_setup_inner close;
	relocate
}
{5}

#ALIAS {start_summon}
{
	#class fight_dragon_cast_inner open;
	#action {^%?%?%?忽然狂风大做，半空现出一条%*龙来！} {#showme {Successfully summoned a dragon.};#class fight_dragon_cast_inner kill;};
	#action {^%?%?%?只有战斗中才能召唤护法。} {#showme {Failed to summon dragon. Not in fight.};#class fight_dragon_cast_inner kill;};
	#action {^%?%?%?你现在不能用魔法！} {#showme {Failed to summon dragon. Casting on cooldown.};#class fight_dragon_cast_inner kill;};
	#action {^%?%?%?你刚叫过护法，他们都被你叫烦了！} {#showme {Failed to summon dragon. Spell on cooldown.};#class fight_dragon_cast_inner kill;};
	#action {你的法力不够了！你的精神无法集中！} {#showme {Failed to summon dragon. Not enough mana.};#class fight_dragon_cast_inner kill;};
	#action {^%?%?%?%?你上一个动作还没有完成，不能念咒文。} {#showme {Failed to summon dragon. I am in busy.};#class fight_dragon_cast_inner kill;};
	#action {但是什么也没有发生。} {#showme {Failed to summon dragon. Invocation failed.};#class fight_dragon_cast_inner kill;};
	#class fight_dragon_cast_inner close;
	cast hufa
}
{5}

#ALIAS {stop}
{
	tkill fight_dragon_fight_inner;
	tkill fight_dragon_setup_inner;
	tkill fight_dragon_cast_inner;
	tkill fight_dragon_move_inner;
	tkill fight_dragon_learn_inner;
	#unticker fight_dragon;
	reset_idle_prevention_ticker
}
{5}

#VARIABLE         {abandon_skills}  {1}
#VARIABLE         {dragons_to_fight}  {4}
#VARIABLE         {fight_enables}  {enable dodge ghost-steps;enable parry liangyi-sword;enable rake skyriver-rake}
#VARIABLE         {learn_skills}  {1}
#VARIABLE         {master}  {cong}
#VARIABLE         {path_from_eat_to_fight}    {w;ne}
#VARIABLE         {path_from_fight_to_eat}    {#2 sw;e}
#VARIABLE         {path_from_fight_to_master}       {#2 sw}
#VARIABLE         {path_from_fight_to_reset}      {#2 sw;#3 se;s}
#VARIABLE         {path_from_master_to_fight}       {ne}
#VARIABLE         {path_from_reset_to_fight}      {n;#3 nw;ne}
#VARIABLE         {path_one_step_back}  {ne}
#VARIABLE         {path_one_step_move}  {#2 sw}
#VARIABLE         {previous_wx}  {0}
#VARIABLE         {previous_qn}  {-1}
#VARIABLE         {skill_to_learn}  {parry}
#VARIABLE         {skill_to_learn2}  {force}
#VARIABLE         {target_skill_level}  {999}
#VARIABLE         {weapon_for_fight}  {rake}
#VARIABLE         {weapon_for_practice}  {}

#CLASS {fight_dragon} CLOSE
