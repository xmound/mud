#read tins/combat.tin
#read tins/default.tin
#class fight_dragon open

#format filename {logs/fight_dragon_log_%s_%t.txt} {$login_id} {%Y-%m-%d};
#log append {$filename} 

#nop settings
#var skill_to_learn {force}
#var skill_to_learn2 {unarmed}
#var auto_reconnect 0
#var previous_wx 0
#var master {cong}

#var path_from_fight_to_reset {#3 sw;#3 se;s}
#var path_from_reset_to_fight {n;#3 nw;ne}

#var path_from_fight_to_eat {#3 sw;e}
#var path_from_eat_to_fight {w;ne}

#var path_from_fight_to_master {#3 sw}
#var path_from_master_to_fight {ne}

#var path_one_step_move {#3 sw}
#var path_one_step_back {ne}

#action {{XYJ RECONNECTION DONE.|XYJ NEW CONNECTION ESTABLISHED.}} {
	stop;
	start_setup;
}

#alias start {
	#echo {<fff> 请确认各技能！};
	#echo {<fff> 请确认各技能！};
	#delay 2 {
		#echo {<fff> 请确认各技能！};
		#echo {<fff> 请确认各技能！};
	};
	start_setup;
}

#alias reset {
	stop;
	tload fight_dragon_$login_id;
	#var previous_wx 0;
	start_fight;
}

#alias stop {
	tkill fight_dragon_fight_inner;
	tkill fight_dragon_setup_inner;
	tkill fight_dragon_cast_inner;
	tkill fight_dragon_move_inner;
	tkill fight_dragon_learn_inner;
	#untick fight_dragon;
}


#alias start_setup {
	#class fight_dragon_setup_inner open;
	
	#nop action {^{一名马官牵着你的坐骑走了过来。|监副对你说：怎么？骑驴找驴？}} {mm;#delay 5 {$path_to_fight;l};};
	#nop action {^{你已经%%*在%*上了！|你潇洒地一个纵身，稳稳地%%*在%%*上！}} {};
	#nop action {^$place_to_fight} {#class fight_dragon_setup_inner kill;start_fight};
	
	#action {^守门人拦住了你：帮派总堂重地闲杂人免进，无帮派人士请去} {
		enforce 100;
		kill guard;
	};
	
	#action {^守门人死了。} {
		enforce 0;
		hd;
		l;
	};
	
	#action {^南天极 -} {enforce 0;kill hd guard;start_summon};
	
	#action {^Failed to summon dragon.} {
		#delay 5 {
			l;
		};
	};

	#action {^Successfully summoned a dragon.} {
		enforce 0;
		#class fight_dragon_setup_inner kill;
		#delay 6 {
			$path_from_reset_to_fight;
			kill dragon;
			start_fight;
		};
	};

	#action {^RELOCATED TO STONE SUCCESSFULLY.} {
		#delay 2 {
			#var previous_wx 0;
			enforce 0;
			set brief_all;
			wimpy 40;
			fly changan;
			#6 s;
			e;
			nu;
			hd;
			l;
		};
	};
	#class fight_dragon_setup_inner close;
	relocate;
}

#alias start_summon {
	#class fight_dragon_cast_inner open;
	
	#action {^%?%?%?忽然狂风大做，半空现出一条%*龙来！} {
		#showme {Successfully summoned a dragon.};
		#class fight_dragon_cast_inner kill;
	};
	
	#action {^%?%?%?只有战斗中才能召唤护法。} {
		#showme {Failed to summon dragon. Not in fight.};
		#class fight_dragon_cast_inner kill;
	};
	
	#action {^%?%?%?你现在不能用魔法！} {
		#showme {Failed to summon dragon. Casting on cooldown.};
		#class fight_dragon_cast_inner kill;
	};
	
	#action {^%?%?%?你刚叫过护法，他们都被你叫烦了！} {
		#showme {Failed to summon dragon. Spell on cooldown.};
		#class fight_dragon_cast_inner kill;
	};
	
	#nop action {你的法力不够了！你的精神无法集中！};

	#action {^%?%?%?%?你上一个动作还没有完成，不能念咒文。} {
		#showme {Failed to summon dragon. I am in busy.};
		#class fight_dragon_cast_inner kill;
	}
	#action {但是什么也没有发生。} {
		#showme {Failed to summon dragon. Invocation failed.};
		#class fight_dragon_cast_inner kill;
	};
	
	#class fight_dragon_cast_inner close;
	cast hufa;
}

#alias start_fight {
	#class fight_dragon_fight_inner open;
	#var count_dragon 0;
	#action {尸体%*Corpse} {#line {gag};};
	#action {骸骨%*Skeleton} {#line {gag};};
	
	#alias check_dragon_status {
		#var count_dragon 0;
		look;
		hp;
		set brief check_dragon_number;
	};
	
	#alias turn_on_fight_dragon_tick {
		#tick fight_dragon {
			check_dragon_status;
		} {30.5};
	};

	#action {^设定环境变数%*check_dragon_number} {
		#if {$count_dragon == 0} {
			#class fight_dragon_fight_inner kill;
			#untick fight_dragon;
			#delay 5 {
				$path_from_fight_to_reset;
				start_setup;
			};
		}; #elseif {$count_dragon < 4} {
			start_summon;
		}; #elseif {$current_qn > 9999999} {
			$path_from_fight_to_master;
			surrender;
			#3 learn $skill_to_learn for 10000;
			re;
			#3 learn $skill_to_learn2 for 10000;
			re;
			$path_from_master_to_fight;
			#loop 1 $count_dragon cnt {fight dragon $cnt};
		}; #elseif {$current_drink < 200 || $current_food < 200} {
				$path_from_fight_to_eat;
				surrender;
				chi;
				$path_from_eat_to_fight;
				sh;
				#loop 1 $count_dragon cnt {kill dragon $cnt;fight dragon $cnt};
		
		};
		
		#if {$previous_wx > 0} {			
			#math wx_gain {$current_wx - $previous_wx};
		}; #elseif {
			#var wx_gain 0;
		};
		
		#math previous_wx  {$current_wx};
		
		#nop turn_on_fight_dragon_tick;
		
		#if {$current_nl_percent > 20} {
			turn_on_fight_dragon_tick;
		}; #else {
			random;
			surrender;
			er;
			fly sky;
			stop;
			start_setup;
		};
		
		#format fight_dragon_status {%t\t%d} {%Y-%m-%d %H:%M:%S} {$wx_gain};
		#format filename {logs/fight_dragon_rewards_%s.txt} {$login_id};
		#line {log} {$filename} {$fight_dragon_status};	
	} {4};
	
	#action {^Successfully summoned a dragon.} {
		#math count_dragon {$count_dragon+1};
		#delay 7 {
			ne;
			surrender;
			sw;
			#loop 1 $count_dragon cnt {fight dragon $cnt;kill dragon $cnt;};
		};
		turn_on_fight_dragon_tick;
		
		#format fight_dragon_status {%t\tSummoned a dragon} {%Y-%m-%d %H:%M:%S};
		#format filename {logs/fight_dragon_rewards_%s.txt} {$login_id};
		#line {log} {$filename} {$fight_dragon_status};	
	};
	
	#prompt {^设定环境变数%*check_dragon_number} {$status_bar_prefix|<fca>Fighting Dragon|目前共$count_dragon只龙|目前武学$current_wx|目前潜能$current_qn|正学习$skill_to_learn} {2};
						
	#action {^%?%?%?%?%*龙%*Dragon} {#math count_dragon {$count_dragon + 1}};
	
	#action {^Failed to summon dragon.} {
		#delay 5 {
			check_dragon_status;
		};
		turn_on_fight_dragon_tick;
	};
	
	#action {^%?%?%?%?你似乎十分疲惫} {
		$path_one_step_move;
		surrender;
		er;
		re;
		#path_one_step_back;
		#loop 1 $count_dragon cnt {fight dragon $cnt};
	} {4};

	#nop action {^%?%?%?%?你气喘嘘嘘} {
		$path_one_step_move;
		surrender;
		er;
		#path_one_step_back;
		#loop 1 $count_dragon cnt {fight dragon $cnt};
	} {4};

	#action {^%?%?%?%?%?%?%?%?%?%?龙气喘嘘嘘} {
		#loop 1 $count_dragon cnt {#2 {exert recover dragon $cnt};};
	};
	
	#action {^%?%?%?%?%?%?%你一次输入} {
		stop;
		#ignore actions on;
		#untick fight_dragon;
		#delay 5 {
			#ignore actions off;
			start;
		};
	};
	
	#action {^%?%?%?%?%?看来该找机会逃跑了} {
		random;
		surrender;
		er;
		fly sky;
		stop;
		start_setup;
	};
	
	#action {{你对准%*深深吸了几口气运将过去|对准你，深深吸了几口气运将过去}} {
		#line {gag};
	};
	
	#class fight_dragon_fight_inner close;
	
	check_dragon_status;
}

#nop alias start_learn {
	#class fight_dragon_learn_inner open;
	#class fight_dragon_learn_inner close;
}

#class fight_dragon close